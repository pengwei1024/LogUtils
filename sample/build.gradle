buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'


android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        applicationId "com.apkfuns.logutils.demo"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions "compile"
    productFlavors {
        // 源码编译
        source {
            dimension "compile"
        }
        // 源码编译 no-op 版本
        sourceNoOp {
            dimension "compile"
        }
        // 二进制编译
        binary {
            dimension "compile"
        }
        // 二进制 no-op 编译
        binaryNoOp {
            dimension "compile"
        }
    }
}

dependencies {
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.squareup.okhttp3:okhttp:3.2.0'
    implementation 'org.apache.commons:commons-lang3:3.4'
    sourceImplementation project(':library')
    sourceImplementation project(':log2file')
    sourceNoOpImplementation project(':logutils-no-op')
    sourceNoOpImplementation project(':log2file')
    binaryImplementation "com.apkfuns.logutils:library:${rootProject.ext.android.releaseVersion}"
    binaryImplementation "com.apkfuns.log2file:log2file:${rootProject.ext.file.releaseVersion}"
    binaryNoOpImplementation "com.apkfuns.logutils:logutils-no-op:${rootProject.ext.android.releaseVersion}"
    binaryNoOpImplementation "com.apkfuns.log2file:log2file:${rootProject.ext.file.releaseVersion}"
}
